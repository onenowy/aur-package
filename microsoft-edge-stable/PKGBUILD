# Maintainer: Nicolas Narvaez <nicomix1006@gmail.com>
# Contributor: EsauPR
# Contributor: bittin

pkgname=microsoft-edge-stable
_pkgname=microsoft-edge
_pkgshortname=msedge
pkgver=144.0.3719.115
pkgrel=1
pkgdesc="A browser that combines a minimal design with sophisticated technology to make the web faster, safer, and easier"
arch=('x86_64')
url="https://www.microsoftedgeinsider.com/en-us/download"
license=('custom')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'systemd' 'dbus' 'libpulse' 'pciutils' 'libva'
         'libffi' 'desktop-file-utils' 'hicolor-icon-theme' 'libxml2')
optdepends=('pipewire: WebRTC desktop sharing under Wayland'
            'kdialog: support for native dialogs in Plasma'
            'gtk4: for --gtk-version=4 (GTK4 IME might work better on Wayland)'
            'org.freedesktop.secrets: password storage backend on GNOME / Xfce / Plasma'
            'upower: Battery Status API support')
options=(!strip !zipman)
source=("https://packages.microsoft.com/yumrepos/edge/Packages/m/${_pkgname}-stable-${pkgver}-1.x86_64.rpm"
        "microsoft-edge-stable.sh")
sha256sums=('e53c294e90c3139aa77dc45d9fce6900849848b72465f2ebc8837fd5d5b14af5'
            'dc3765d2de6520b13f105b8001aa0e40291bc9457ac508160b23eea8811e26af')

package() {

	cp --parents -a {opt,usr} "$pkgdir"
	# suid sandbox
	chmod 4755 "${pkgdir}/opt/microsoft/${_pkgshortname}/msedge-sandbox"
	# install icons
	for res in 16 24 32 48 64 128 256; do
		install -Dm644 "${pkgdir}/opt/microsoft/${_pkgshortname}/product_logo_${res}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/${_pkgname}.png"
	done
       # User flag aware launcher
       install -m755 microsoft-edge-stable.sh "${pkgdir}/usr/bin/microsoft-edge-stable"

       rm "${pkgdir}/opt/microsoft/${_pkgshortname}"/product_logo_*.png

       rm -f "${pkgdir}/usr/share/applications/com.microsoft.Edge.desktop"
       mv "${pkgdir}/usr/share/applications/microsoft-edge.desktop" "${pkgdir}/usr/share/applications/com.microsoft.Edge.desktop"
}
