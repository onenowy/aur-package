# Maintainer: envolution
# Contributor: SoleSoul
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=lm-studio
pkgver=0.3.36.1
pkgrel=1
pkgdesc="Discover, download, and run local LLMs"
arch=('x86_64')
url="https://lmstudio.ai/"
license=('LicenseRef-EULA')
depends=('zlib' 'hicolor-icon-theme' 'fuse2' 'clblast')
makedepends=('squashfs-tools' 'graphicsmagick')
options=(!strip !debug)

_basever="${pkgver%.*}"
_build="${pkgver##*.}"
_filename="LM-Studio-${_basever}-${_build}-x64.AppImage"

source=("${pkgname}-${pkgver}.AppImage::https://installers.lmstudio.ai/linux/x64/${_basever}-${_build}/${_filename}")
sha256sums=('102bb6cf683721ff7e2443fba43d85775993564d4a9ba3702a7ba4c92de77e1c')
noextract=("${pkgname}-${pkgver}.AppImage")

prepare() {
  rm -rf squashfs-root
  chmod +x "${pkgname}-${pkgver}.AppImage"
  offset=$(./"${pkgname}-${pkgver}.AppImage" --appimage-offset)
  unset PAGER
  unsquashfs -q -o "$offset" -d squashfs-root "${pkgname}-${pkgver}.AppImage" \
    "${pkgname}.desktop" \
    "usr/share/icons/hicolor/0x0/apps/lm-studio.png" \
    "LICENSE.electron.txt" \
    "LICENSES.chromium.html"
}

build() {
  sed -i -E "s|Exec=AppRun|Exec=env DESKTOPINTEGRATION=false /usr/bin/${pkgname}|" \
    "squashfs-root/${pkgname}.desktop"
  sed -i -E "s|Icon=.*|Icon=${pkgname}|" \
    "squashfs-root/${pkgname}.desktop"
}

package() {
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}.AppImage" "${pkgdir}/opt/${pkgname}/${pkgname}.AppImage"
  install -Dm644 "${srcdir}/squashfs-root/LICENSE"* -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/EULA" <<< "https://lmstudio.ai/app-terms"

  install -Dm644 "${srcdir}/squashfs-root/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  src_icon="$srcdir/squashfs-root/usr/share/icons/hicolor/0x0/apps/lm-studio.png"
  sizes=("16x16" "32x32" "48x48" "64x64" "128x128" "256x256")

  for size in "${sizes[@]}"; do
    install -dm755 "${pkgdir}/usr/share/icons/hicolor/$size/apps"
    gm convert "$src_icon" -resize "$size" "$pkgdir/usr/share/icons/hicolor/$size/apps/${pkgname}.png"
  done

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/${pkgname}.AppImage" "${pkgdir}/usr/bin/${pkgname}"
}
# vim:set ts=2 sw=2 et:
